alias functionNum R1;

if(functionNum == 1) then         //Get Free Page
  [SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] + 1;

  while([SYSTEM_STATUS_TABLE + 2] == 0) do
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 4] = WAIT_MEM;

    multipush(R1);
    call MOD_5;
    multipop(R1);
  endwhile;

  [SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;
  [SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] - 1;

  alias counter R0;
  counter = 0;
  while([MEMORY_FREE_LIST + counter] != 0) do
    counter = counter + 1;
  endwhile;
  [MEMORY_FREE_LIST + counter] = 1;
endif;

if(functionNum == 2) then         //Release Page
  [MEMORY_FREE_LIST + R2] = [MEMORY_FREE_LIST + R2] - 1;
  if([MEMORY_FREE_LIST + R2] == 0) then
    [SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] + 1;

    alias counter R5;
    counter = 0;
    while(counter < 16) do
      if([PROCESS_TABLE + counter*16 + 4] == WAIT_MEM) then
        [PROCESS_TABLE + counter*16 + 4] = READY;
      endif;
      counter = counter + 1;
    endwhile;
  endif;
endif;

if(functionNum == 3) then         //Get Free Block
  alias counter R0;
  counter = 69;
  while(counter < 256) do
    if([DISK_FREE_LIST + counter] == 0) then
      [DISK_FREE_LIST + counter] = 1;
      break;
    endif;
    counter = counter + 1;
  endwhile;

  if(counter == 256) then
    counter = -1;
  endif;
endif;

if(functionNum == 4) then         //Release Block
  [DISK_FREE_LIST + R2] = [DISK_FREE_LIST + R2] - 1;
endif;

if(functionNum == 5) then         //Get Code Page
  alias counter R3;
  alias page R4;
  counter = 0;
  while(counter < 160) do
    page = [PAGE_TABLE_BASE + (counter/10)*20 + 2*(counter%10)];
    if([DISK_MAP_TABLE + counter] == R2 && page != -1) then
      [MEMORY_FREE_LIST + page] = [MEMORY_FREE_LIST + page] + 1;
      R0 = page;
      break;
    endif;
    counter = counter + 1;
  endwhile;

  alias block R5;
  block = R2;
  if(counter == 160) then
    multipush(R1, R2, R3);
    R1 = 1;
    call MOD_2;
    page = R0;
    multipop(R1, R2, R3);

    multipush(R1, R2, R3, R4);
    R1 = 2;
    R2 = [SYSTEM_STATUS_TABLE + 1];
    R3 = page;
    R4 = block;
    call MOD_4;
    multipop(R1, R2, R3, R4);

    R0 = page;
  endif;
endif;

if(functionNum == 6) then         //Get Swap Block
  alias counter R3;
  counter = 256;
  while(counter < 512) do
    if([DISK_FREE_LIST + counter] == 0) then
      break;
    endif;
    counter = counter + 1;
  endwhile;

  if(counter == 512) then
    R0 = -1;
    return;
  endif;

  [DISK_FREE_LIST + counter] = 1;
  R0 = counter;
endif;

return;
