[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 5;

alias userSP R0;
userSP = SP;

[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 13] = SP;
SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 11]*512 - 1;

alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;
alias fileDesc R4;
physicalPageNum = [PTBR + 2*(userSP - 4)/512];
offset = (userSP - 4)%512;
fileDescPhysicalAddr = physicalPageNum*512 + offset;
fileDesc = [fileDescPhysicalAddr];

if (fileDesc != -2) then
  alias userAreaPageNum R5;
  userAreaPageNum = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 11];

  if(fileDesc < 0 || fileDesc >= 8 || [userAreaPageNum*512 + 496 + 2*fileDesc] != FILE) then
    alias physicalAddrRetVal R5;
    physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
    [physicalAddrRetVal] = -1;

    SP = userSP;
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
    ireturn;
  endif;

  alias fileIndex R6;
  fileIndex = [userAreaPageNum*512 + 496 + 2*fileDesc + 1];

  alias inodeIndex R7;
  inodeIndex = [OPEN_FILE_TABLE + fileIndex*4];

  if([INODE_TABLE + inodeIndex*16 + 3] != [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 3]
    && [INODE_TABLE + inodeIndex*16 + 3] != 1
    && [INODE_TABLE + inodeIndex*16 + 4] == EXCLUSIVE) then
    alias physicalAddrRetVal R8;
    physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
    [physicalAddrRetVal] = -3;

    SP = userSP;
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
    ireturn;
  endif;

  multipush(R4, R5, R6, R7, R0);
  R1 = 4;
  R2 = inodeIndex;
  R3 = [SYSTEM_STATUS_TABLE + 1];
  call MOD_0;
  if(R0 == -1) then
    multipop(R0);

    alias physicalAddrRetVal R8;
    physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
    [physicalAddrRetVal] = -1;

    SP = userSP;
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
    ireturn;
  endif;
  multipop(R4, R5, R6, R7, R0);

  alias Lseek R8;
  Lseek = [OPEN_FILE_TABLE + fileIndex*4 + 2];

  if(Lseek == 2048) then
    backup;
    R1 = 5;
    R2 = inodeIndex;
    R3 = [SYSTEM_STATUS_TABLE + 1];
    call MOD_0;
    restore;

    alias physicalAddrRetVal R9;
    physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
    [physicalAddrRetVal] = -2;

    SP = userSP;
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
    ireturn;
  endif;

  if(Lseek%512 == 0 && Lseek == [INODE_TABLE + inodeIndex*16 + 2]) then
    alias block R9;

    multipush(R0, R4, R5, R6, R7, R8);
    R1 = 3;
    call MOD_2;
    block = R0;
    multipop(R0, R4, R5, R6, R7, R8);

    if(block == -1) then
      multipush(R0, R4, R5, R6, R7, R8);
      R1 = 5;
      R2 = inodeIndex;
      R3 = [SYSTEM_STATUS_TABLE + 1];
      call MOD_0;
      multipop(R0, R4, R5, R6, R7, R8);

      alias physicalAddrRetVal R10;
      physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
      [physicalAddrRetVal] = -2;

      SP = userSP;
      [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
      ireturn;
    endif;

    [INODE_TABLE + inodeIndex*16 + (Lseek/512) + 8] = block;
  endif;

  alias word R9;
  word = [[PTBR + 2*(userSP - 3)/512]*512 + (userSP - 3)%512];

  multipush(R0, R4, R5, R6, R7, R8, R9);
  R1 = 1;
  R2 = [INODE_TABLE + inodeIndex*16 + Lseek/512 + 8];
  R3 = Lseek%512;
  R4 = word;
  call MOD_3;
  multipop(R0, R4, R5, R6, R7, R8, R9);

  if(Lseek == [INODE_TABLE + inodeIndex*16 + 2]) then
    [INODE_TABLE + inodeIndex*16 + 2] = Lseek + 1;
    [ROOT_FILE + inodeIndex*8 + 1] = Lseek + 1;
  endif;

  [OPEN_FILE_TABLE + fileIndex*4 + 2] = Lseek + 1;

  multipush(R0, R4, R5, R6, R7, R8, R9);
  R1 = 5;
  R2 = inodeIndex;
  R3 = [SYSTEM_STATUS_TABLE + 1];
  call MOD_0;
  multipop(R0, R4, R5, R6, R7, R8, R9);
else
  alias word R5;
  word = [[PTBR + 2*(userSP - 3)/512]*512 + (userSP - 3)%512];

  multipush(R0, R1, R2, R3, R4, R5);
  R1 = 3;
  R2 = [SYSTEM_STATUS_TABLE + 1];
  R3 = word;
  call MOD_4;
  multipop(R0, R1, R2, R3, R4, R5);
endif;

alias physicalAddrRetVal R5;
physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
[physicalAddrRetVal] = 0;

SP = userSP;
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
ireturn;
