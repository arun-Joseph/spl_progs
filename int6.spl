[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 7;

alias userSP R0;
userSP = SP;

[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 13] = SP;
SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 11]*512 - 1;

alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;
alias fileDesc R4;
physicalPageNum = [PTBR + 2*(userSP - 4)/512];
offset = (userSP - 4)%512;
fileDescPhysicalAddr = physicalPageNum*512 + offset;
fileDesc = [fileDescPhysicalAddr];

if(fileDesc != -1) then
  alias userAreaPageNum R5;
  userAreaPageNum = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 11];

  if(fileDesc < 0 || fileDesc >= 8 || [userAreaPageNum*512 + 496 + 2*fileDesc] != FILE) then
    alias physicalAddrRetVal R5;
    physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
    [physicalAddrRetVal] = -1;

    SP = userSP;
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
    ireturn;
  endif;

  alias fileIndex R6;
  fileIndex = [userAreaPageNum*512 + 496 + 2*fileDesc + 1];

  alias inodeIndex R7;
  inodeIndex = [OPEN_FILE_TABLE + fileIndex*4];

  multipush(R4, R5, R6, R7, R0);
  R1 = 4;
  R2 = inodeIndex;
  R3 = [SYSTEM_STATUS_TABLE + 1];
  call MOD_0;
  if(R0 == -1) then
    multipop(R0);

    alias physicalAddrRetVal R8;
    physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
    [physicalAddrRetVal] = -1;

    SP = userSP;
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
    ireturn;
  endif;
  multipop(R4, R5, R6, R7, R0);

  alias Lseek R8;
  Lseek = [OPEN_FILE_TABLE + fileIndex*4 + 2];

  alias word R9;
  word = [[PTBR + 2*(userSP - 3)/512]*512 + (userSP - 3)%512];

  if([OPEN_FILE_TABLE + fileIndex*4] == INODE_ROOT) then
    if(Lseek == 480) then
      multipush(R0, R4, R5, R6, R7, R8, R9);
      R1 = 5;
      R2 = inodeIndex;
      R3 = [SYSTEM_STATUS_TABLE + 1];
      call MOD_0;
      multipop(R0, R4, R5, R6, R7, R8, R9);

      alias physicalAddrRetVal R8;
      physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
      [physicalAddrRetVal] = -2;

      SP = userSP;
      [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
      ireturn;
    endif;

    [[PTBR + 2*(word/512)]*512 + word%512] = [ROOT_FILE + Lseek];
  else
    alias fileSize R10;
    fileSize = [INODE_TABLE + inodeIndex*16 + 2];

    if(Lseek == fileSize) then
      multipush(R0, R4, R5, R6, R7, R8, R9);
      R1 = 5;
      R2 = inodeIndex;
      R3 = [SYSTEM_STATUS_TABLE + 1];
      call MOD_0;
      multipop(R0, R4, R5, R6, R7, R8, R9);

      alias physicalAddrRetVal R8;
      physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
      [physicalAddrRetVal] = -2;

      SP = userSP;
      [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
      ireturn;
    endif;

    multipush(R0, R4, R5, R6, R7, R8, R9, R10);
    R1 = 2;
    R2 = [INODE_TABLE + inodeIndex*16 + Lseek/512 + 8];
    R3 = Lseek%512;
    R4 = [PTBR + 2*(word/512)]*512 + word%512;
    call MOD_3;
    multipop(R0, R4, R5, R6, R7, R8, R9, R10);
  endif;

  [OPEN_FILE_TABLE + fileIndex*4 + 2] = Lseek + 1;

  multipush(R0, R4, R5, R6, R7, R8, R9);
  R1 = 5;
  R2 = inodeIndex;
  R3 = [SYSTEM_STATUS_TABLE + 1];
  call MOD_0;
  multipop(R0, R4, R5, R6, R7, R8, R9);
else
  alias word R5;
  word = [[PTBR + 2*(userSP - 3)/512]*512 + (userSP - 3)%512];

  multipush(R0, R1, R2, R3, R4, R5);
  R1 = 4;
  R2 = [SYSTEM_STATUS_TABLE + 1];
  R3 = word;
  call MOD_4;
  multipop(R0, R1, R2, R3, R4, R5);
endif;

alias physicalAddrRetVal R5;
physicalAddrRetVal = [PTBR + 2*(userSP - 1)/512]*512 + (userSP - 1)%512;
[physicalAddrRetVal] = 0;

SP = userSP;
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 9] = 0;
ireturn;
