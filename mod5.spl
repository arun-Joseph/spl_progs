alias currentPID R0;
currentPID = [SYSTEM_STATUS_TABLE + 1];
multipush(BP);

alias process_table_entry R1;
process_table_entry = PROCESS_TABLE + currentPID*16;

[process_table_entry + 12] = SP%512;
[process_table_entry + 14] = PTBR;
[process_table_entry + 15] = PTLR;

alias counter R2;
alias newPID R3;
if([SYSTEM_STATUS_TABLE + 5] != -1) then
  newPID = [SYSTEM_STATUS_TABLE + 5];
  if([PROCESS_TABLE + newPID*16 + 4] == WAIT_DISK) then
    newPID = 0;
  endif;
else
  counter = (currentPID + 1)%16;
  while(counter != currentPID) do
    if(counter == 0) then
      counter = 1;
      continue;
    endif;

    if(counter == 15) then
      counter = 0;
      continue;
    endif;

    if(([PROCESS_TABLE + counter*16 + 4] == READY || [PROCESS_TABLE + counter*16 + 4] == CREATED)
      && [PROCESS_TABLE + counter*16 + 6] == 0) then
      break;
    endif;
    counter = (counter + 1)%16;
  endwhile;

  if(counter == currentPID) then
    if(currentPID == 0 &&
      ([PROCESS_TABLE + 15*16 + 4] == READY || [PROCESS_TABLE + 15*16 + 4] == CREATED)) then
      newPID = 15;
    else
      if([PROCESS_TABLE + counter*16 + 4] == READY || [PROCESS_TABLE + counter*16 + 4] == CREATED) then
        newPID = counter;
      else
        newPID = 0;
      endif;
    endif;
  else
    newPID = counter;
  endif;
endif;

alias new_process_table R4;
new_process_table = PROCESS_TABLE + newPID*16;
SP = [new_process_table + 11]*512 + [new_process_table + 12];
PTBR = [new_process_table + 14];
PTLR = [new_process_table + 15];
[SYSTEM_STATUS_TABLE + 1] = newPID;

if([new_process_table + 4] == CREATED) then
  [new_process_table + 4] = RUNNING;
  [new_process_table + 9] = 0;
  SP = [new_process_table + 13];
  BP = [[new_process_table + 11]*512];
  ireturn;
endif;

[new_process_table + 4] = RUNNING;
multipop(BP);
return;
